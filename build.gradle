plugins {
    id 'org.jenkins-ci.jpi' version '0.49.0'
    id 'jacoco'
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

if (JavaVersion.current() != JavaVersion.VERSION_11) {
    throw new GradleException("Build requires Java ${JavaVersion.VERSION_11}")
}

def coreBaseVersion = '2.361'

jenkinsPlugin {
    coreVersion = coreBaseVersion
    shortName = project.name
    displayName = jenkinsDisplayName
    url = "https://github.com/jenkinsci/$project.name"
    gitHubUrl = "https://github.com/jenkinsci/$project.name"
    compatibleSinceVersion = '1.20'
    fileExtension = 'hpi'
    maskClasses = 'groovy.grape'
    pluginFirstClassLoader = true
    developers {
        developer {
            id 'nickg'
            name 'Nick Grealy'
            email 'nickgrealy@gmail.com'
        }
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.4.21' // add the Groovy lib to the plugin to make @Grab work
    implementation 'org.apache.ivy:ivy:2.4.0'              // required for @Grab

    testImplementation 'io.cucumber:cucumber-junit:7.14.0'
    testImplementation 'io.cucumber:cucumber-java:7.14.0'
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    testRuntimeOnly("org.jenkins-ci.main:jenkins-war:${coreBaseVersion}")
}

defaultTasks 'clean', 'jpi'

clean {
    delete 'work'
}

tasks.register('deleteTarget', Delete) {
    delete 'target', 'out', 'work'
}

jacocoTestReport {
    reports {
        xml.required
        html.required
    }
}

check.dependsOn jacocoTestReport

// Integration Test Structure
test {
    exclude '**/integration/**'
    reports {
        junitXml.required
        html.required
    }
}

tasks.register('integTest', Test) {
    dependsOn = [jpi, test]
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include '**/integration/**'
    outputs.upToDateWhen { false }
    reports {
        junitXml.required
        html.required
    }
}

check.dependsOn 'integTest'

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }
}

clean.dependsOn deleteTarget

wrapper {
    gradleVersion = '8.4'
}
